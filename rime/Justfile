rime-dir := canonicalize(config_directory() + "/ibus/rime")
cantonese-dir := data_directory() + "/rime-cantonese"

default:
	just --choose

install:
	#!/usr/bin/env fish
	pushd {{rime-dir}}
	if not git status &> /dev/null
		echo "Cloning oh-my-rime..."
		git clone https://github.com/Mintimate/oh-my-rime.git
		cp -r oh-my-rime/. {{rime-dir}}
		rm -rf oh-my-rime
		echo "Cloned repository"
	else
		echo "Already cloned oh-my-rime."
	end
	popd

	mkdir -p {{cantonese-dir}}
	pushd {{cantonese-dir}}
	if not git status &> /dev/null
		echo "Cloning rime-cantonese..."
		git clone https://github.com/rime/rime-cantonese.git .
		echo "Cloned repository"
	else
		echo "Already cloned rime-cantonese."
	end
	popd

update:
	#!/usr/bin/env fish
	pushd {{rime-dir}}
	git pull
	popd

	pushd {{cantonese-dir}}
	git pull
	popd

link:
	#!/usr/bin/env fish
	set -l filenames (find {{justfile_directory()}}/patches -type f -name '*.custom.yaml')
	for src in $filenames
		set_color -b cyan; set_color black; echo -n " Linking "; set_color normal
		set -l filename (basename $src)
		set -l dst "{{rime-dir}}/$filename"
		echo " $src -> $dst"
		ln -sf $src $dst
	end
	pushd {{cantonese-dir}}
	set -l filenames jyut6ping3*.yaml essay-cantonese.txt symbols_cantonese.yaml opencc/*
	for f in $filenames
		set_color -b cyan; set_color black; echo -n " Linking "; set_color normal
		set -l src (realpath $f)
		set -l dst "{{rime-dir}}/$f"
		echo " $src -> $dst"
		ln -sf $src $dst
	end
	popd
	set_color -b green; set_color black; echo " Done "; set_color normal

clean:
	#!/usr/bin/env fish
	for link in {{rime-dir}}/*.custom.yaml
		if readlink $link > /dev/null
			set_color -b red; set_color black; echo -n " Remove "; set_color normal
			echo " $link"
			unlink $link
		end
	end
	set_color -b green; set_color black; echo " Done "; set_color normal
